VERSION(:)"
dict 20.07.120
Copyright (C) 2020 Tristan Styles
MIT License
";
USAGE(:)"
dict [options] FILE...
options:
	-h, --help             display this help
	-v, --verbose          display file names
	-d, --directory DIR    set input DIRectory
	-o, --output FILE      output words to FILE
	-m, --minimal          only read the minimum necessary data
	-n, --noaffix          do not expand affixes
	-p, --noprefix         do not expand prefixes
	-s, --nosuffix         do not expand suffixes
	-c, --nocontin         do not expand continuations
	-w, --nowords          do not output words
	    --version          display program version
	    --debug            display debugging information

Expands a dictionary file to produce affixed word variants.

Where FILE is the root name of the file(s) to be processed. More than one file
may be given; each file will be processed in turn.

If FILE is prefixed with @ then it provides a list of files to be processed,
with one file per line.
";

#
# Tested with dictionaries from https://github.com/wooorm/dictionaries
#
# Uses a lot of memory, some dictionaries can cause a program crash.
#

#==============================================================================

"htmlchar"@import;

#==============================================================================

"=" "[=]"(L,R): L[L@length] = R;
"="  "=:"(L,R): (T:L, L = R, T);
"+"  "++"(L)  : L += 1;
"-"  "--"(L)  : L -= 1;

__:[
	enum:0;
];

enum(e): (e@is_Integer ? (r:__(enum) = e ; r:__(enum)), __(enum++), r);

lengthof(X): (X@is_Zen ! (X@length ; 0));

FALSE(:)?;
TRUE (:)!;

#==============================================================================

unable_to_open file: ("unable to open ", file)@printerr;
unable_to_read file: ("unable to read ", file)@printerr;

outfile :(">", "w")@open;
minimal :FALSE;
noaffix :FALSE;
noprefix:FALSE;
nosuffix:FALSE;
nocontin:FALSE;
nowords :FALSE;
verbose :FALSE;
debug   :FALSE;

#==============================================================================

dump(name,object,indent:): (
	object@is_Environment ? ((
		('\t' indent, name, ":[]")@println,
		(N:object@length, i:0; i < N; i++) ?* (
			dump(object[i]@identifier, object[i], indent+1)
		)
	);(
		object@is_String ? ((
			('\t' indent, name, ":\"", object, "\"")@println,
		);(
			('\t' indent, name, ':', object)@println,
		))
	))
);

getln(f): (
	line:f@fgetln,
	line@is_String ? (
		line,
	;
		""
	)
);

gettok(s): (
	n:s@span_NotSpace,
	t:s <<< n,
	n += (s(n..))@span_Space,
	s <<= n,
	t
);

split1(arg): (
	s:gettok(arg),
	s
);

split2(arg): (
	s:gettok(arg),
	t:gettok(arg),
	[s, t]
);

split3(arg): (
	s:gettok(arg),
	t:gettok(arg),
	z:gettok(arg),
	[s, t, z]
);

splitw(arg): (
	return:[],

	arg ?* (
		n:arg@span_Space,
		n ? (
			arg <<= n
		),
		n:arg@span_NotSpace,
		n ? (
			return [=] arg <<< n,
			arg <<= n
		)
	),

	return
);

splitc(arg,set): (
	return:[],

	arg ?* (
		n:(arg, set)@span_InSet,
		n ? (
			arg <<= n
		),
		n:(arg, set)@span_NotInSet,
		n ? (
			return [=] arg <<< n,
			arg <<= n
		)
	),

	return
);

#==============================================================================

(
	NUM_FLAG (:)enum(0),
	CHAR_FLAG(:)enum(),
	LONG_FLAG(:)enum()
);
(
	enum(0),
	WARN              (:)enum(),
	NOSUGGEST         (:)enum(),
	COMPOUNDFLAG      (:)enum(),
	COMPOUNDBEGIN     (:)enum(),
	COMPOUNDLAST      (:)enum(),
	COMPOUNDMIDDLE    (:)enum(),
	ONLYINCOMPOUND    (:)enum(),
	COMPOUNDPERMITFLAG(:)enum(),
	COMPOUNDFORBIDFLAG(:)enum(),
	COMPOUNDROOT      (:)enum(),
	FORCEUCASE        (:)enum(),
	SYLLABLENUM       (:)enum(),
	CIRCUMFIX         (:)enum(),
	FORBIDDENWORD     (:)enum(),
	KEEPCASE          (:)enum(),
	LEMMA_PRESENT     (:)enum(),
	NEEDAFFIX         (:)enum(),
	PSEUDOROOT        (:)enum(),
	SUBSTANDARD       (:)enum(),
);
(
	ARC_SEQ(:)enum(0),
	ARC_SET(:)enum(),
	ARC_NOT(:)enum()
);

AFFIX(:)[
	COUNT               : 0,
	NAME                : "",
	VERSION             : "",
	HOME                : "",
	SET                 : "UTF-8",
	FLAG                : [type: CHAR_FLAG],
	LANG                : "",
	IGNORE              : "",
	TRY                 : "",
	KEY                 : [],
	MAP                 : [],
	AF                  : [""],
	AM                  : [""],
	REP                 : [],
	PFX                 : [],
	SFX                 : [],
	BREAK               : ["-", "^-", "-$"],
	PHONE               : [],
	FORBIDWARN          : FALSE,
	COMPLEXPREFIXES     : FALSE,
	MAXCPDSUGS          : 0,
	MAXNGRAMSUGS        : 0,
	MAXDIFF             : 0,
	ONLYMAXDIFF         : FALSE,
	NOSPLITSUGS         : FALSE,
	SUGSWITHDOTS        : FALSE,
	COMPOUNDRULE        : [],
	COMPOUNDMIN         : 0,
	COMPOUNDWORDMAX     : 0,
	CHECKCOMPOUNDDUP    : FALSE,
	CHECKCOMPOUNDREP    : FALSE,
	CHECKCOMPOUNDCASE   : FALSE,
	CHECKCOMPOUNDTRIPLE : FALSE,
	SIMPLIFIEDTRIPLE    : FALSE,
	CHECKCOMPOUNDPATTERN: [],
	COMPOUNDSYLLABLE    : [],
	FULLSTRIP           : FALSE,
	ICONV               : [],
	OCONV               : [],
	WORDCHARS           : "",
	CHECKSHARPS         : FALSE
];

flag_type(arg): (
	arg ?: (
	"num" : NUM_FLAG;
	"long": LONG_FLAG;
	        CHAR_FLAG
	)
);

get_affix_flags(s,n:): (
	flag:[],

	N(:)s@length,
	n ? ((
		(start:0, end:(n - 1); start < N; start += n, end += n) ?* (
			flag [=] s[start..end]
		)
	);(
		(start:0; start < N; start += n + 1) ?* (
			n = (s(start..))@span_Digit,
			n ? (
				flag [=] s[start..(start + n - 1)]
			)
		)
	)),

	flag
);

set_flag(afx,arg,flag): (
	afx[arg] = flag
);

set_value(afx,arg): (
	afx = arg
);

set_string(afx,arg): (
	afx = arg@to_String
);

set_number(afx,arg): (
	afx = arg@to_Integer
);

set_boolean(afx): (
	afx = TRUE
);

set_affix_class(afx,field,count) : (
	afx[field[0]] = [
		cross:(field[1] == "Y"),
		rule :[]
	],
	count = field[2]@to_Integer
);

set_affix_rule_condition(field): (
	cond:[len:0],

	field <> "." ? ((N(:)lengthof(field), start:0, end:; start < N; start = end) ?* (
		end = start + (field(start..), "[")@span_NotInSet,
		end > start ? ((
			cond [=] [
				char:field[start..(end - 1)],
				type:0
			],
			cond len += (end - start)

		);(field[start] == '[' ? (
			start += 1,
			invert:(start < N) && (field[start] == '^'),
			start += invert,

			end = start + (field(start..), "]")@span_NotInSet,
			end > start ? (
				cond [=] [
					char:field[start..(end - 1)],
					type:invert ? (2 ; 1)
				],
				cond len += 1,

				end += (end < N) && (field[end] == ']')
			)
		)))
	)),

	cond
);

set_affix_rule_strip(field): (
	field <> "0" ? (field ; ())
);

set_affix_rule_affix(field): (
	n:(field, "/")@span_NotInSet,
	n < field@length ? (
		[field <<< n, get_affix_flags(field << (n + 1), __(affix)FLAG type)]
	;
		[field <<< n, ()]
	)
);

set_affix_rule(afx,field,count): (
	afx[field[0]]rule [=] [
		strip:           set_affix_rule_strip(field[1]),
		affix:           set_affix_rule_affix(field[2]),
		cond :           set_affix_rule_condition(field[3]),
		morph: minimal ! field@length > 4 ? (field[4..] ; ())
	],
	count--
);

set_affix(afx,field,count): (
	count ! (
		set_affix_class(afx,field,count)
	;
		set_affix_rule(afx,field,count)
	)
);

set_counted(afx,field,count): (
	count ! (
		count = arg@to_Integer
	;
		afx [=] field,
		count--
	)
);

affix_cmd:[
	NAME                (arg): minimal ! set_string (__(affix)NAME                , arg);
	VERSION             (arg): minimal ! set_string (__(affix)VERSION             , arg);
	HOME                (arg): minimal ! set_string (__(affix)HOME                , arg);
	SET                 (arg): minimal ! set_string (__(affix)SET                 , split1 arg);
	FLAG                (arg):           set_value  (__(affix)FLAG type           , split1 arg  flag_type);
	LANG                (arg): minimal ! set_string (__(affix)LANG                , split1 arg);
	IGNORE              (arg): minimal ! set_string (__(affix)IGNORE              , split1 arg);
	TRY                 (arg): minimal ! set_string (__(affix)TRY                 , split1 arg);
	KEY                 (arg): minimal ! set_value  (__(affix)KEY                 , splitc(arg, "|"));
	MAP                 (arg): minimal ! set_counted(__(affix)MAP                 , split1 arg, __(affix)COUNT);
	AF                  (arg):           set_counted(__(affix)AF                  , split1 arg, __(affix)COUNT);
	AM                  (arg): minimal ! set_counted(__(affix)AM                  , split1 arg, __(affix)COUNT);
	REP                 (arg): minimal ! set_counted(__(affix)REP                 , split2 arg, __(affix)COUNT);
	PFX                 (arg):           set_affix  (__(affix)PFX                 , splitw arg, __(affix)COUNT);
	SFX                 (arg):           set_affix  (__(affix)SFX                 , splitw arg, __(affix)COUNT);
	BREAK               (arg): minimal !(set_counted(__(affix)BREAK               , split1 arg, __(affix)COUNT), __(affix)COUNT ! (__(affix)BREAK = []));
	PHONE               (arg): minimal ! set_counted(__(affix)PHONE               , split2 arg, __(affix)COUNT);
	WARN                (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, WARN);
	FORBIDWARN          (arg): minimal ! set_boolean(__(affix)FORBIDWARN          );
	COMPLEXPREFIXES     (arg): minimal ! set_boolean(__(affix)COMPLEXPREFIXES     );
	NOSUGGEST           (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, NOSUGGEST);
	MAXCPDSUGS          (arg): minimal ! set_number (__(affix)MAXCPDSUGS          , split1 arg);
	MAXNGRAMSUGS        (arg): minimal ! set_number (__(affix)MAXNGRAMSUGS        , split1 arg);
	MAXDIFF             (arg): minimal ! set_number (__(affix)MAXDIFF             , split1 arg);
	ONLYMAXDIFF         (arg): minimal ! set_boolean(__(affix)ONLYMAXDIFF         );
	NOSPLITSUGS         (arg): minimal ! set_boolean(__(affix)NOSPLITSUGS         );
	SUGSWITHDOTS        (arg): minimal ! set_boolean(__(affix)SUGSWITHDOTS        );
	COMPOUNDRULE        (arg): minimal ! set_counted(__(affix)COMPOUNDRULE        , split1 arg, __(affix)COUNT);
	COMPOUNDMIN         (arg): minimal ! set_number (__(affix)COMPOUNDMIN         , split1 arg);
	COMPOUNDFLAG        (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDFLAG);
	COMPOUNDBEGIN       (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDBEGIN);
	COMPOUNDLAST        (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDLAST);
	COMPOUNDMIDDLE      (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDMIDDLE);
	ONLYINCOMPOUND      (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, ONLYINCOMPOUND);
	COMPOUNDPERMITFLAG  (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDPERMITFLAG);
	COMPOUNDFORBIDFLAG  (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDFORBIDFLAG);
	COMPOUNDROOT        (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, COMPOUNDROOT);
	COMPOUNDWORDMAX     (arg): minimal ! set_number (__(affix)COMPOUNDWORDMAX     , split1 arg);
	CHECKCOMPOUNDDUP    (arg): minimal ! set_boolean(__(affix)CHECKCOMPOUNDDUP    );
	CHECKCOMPOUNDREP    (arg): minimal ! set_boolean(__(affix)CHECKCOMPOUNDREP    );
	CHECKCOMPOUNDCASE   (arg): minimal ! set_boolean(__(affix)CHECKCOMPOUNDCASE   );
	CHECKCOMPOUNDTRIPLE (arg): minimal ! set_boolean(__(affix)CHECKCOMPOUNDTRIPLE );
	SIMPLIFIEDTRIPLE    (arg): minimal ! set_boolean(__(affix)SIMPLIFIEDTRIPLE    );
	CHECKCOMPOUNDPATTERN(arg): minimal ! set_counted(__(affix)CHECKCOMPOUNDPATTERN, splitw arg, __(affix)COUNT);
	FORCEUCASE          (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, FORCEUCASE);
	COMPOUNDSYLLABLE    (arg): minimal ! set_value  (__(affix)COMPOUNDSYLLABLE    , split2 arg);
	SYLLABLENUM         (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, SYLLABLENUM);
	CIRCUMFIX           (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, CIRCUMFIX);
	FORBIDDENWORD       (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, FORBIDDENWORD);
	FULLSTRIP           (arg): minimal ! set_boolean(__(affix)FULLSTRIP           );
	ICONV               (arg): minimal ! set_counted(__(affix)ICONV               , split2 arg, __(affix)COUNT);
	OCONV               (arg): minimal ! set_counted(__(affix)OCONV               , split2 arg, __(affix)COUNT);
	KEEPCASE            (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, KEEPCASE);
	LEMMA_PRESENT       (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, LEMMA_PRESENT);
	NEEDAFFIX           (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, NEEDAFFIX);
	PSEUDOROOT          (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, PSEUDOROOT);
	SUBSTANDARD         (arg): minimal ! set_flag   (__(affix)FLAG                , split1 arg, SUBSTANDARD);
	WORDCHARS           (arg): minimal ! set_string (__(affix)WORDCHARS           , split1 arg);
	CHECKSHARPS         (arg): minimal ! set_boolean(__(affix)CHECKSHARPS         );
];

get_affix_args(s,o:): (
	n:o + (s(o..))@span_Space,
	m:n + (s(n..), "#")@span_NotInSet,
	n < m ? (
		o  = (s(n..m))@span_RevSpace,
		m -= o
	),
	n < m ? (
		s[n..(m - 1)]
	;
		""
	)
);

read_affix(file_aff,affix): (
	f:file_aff@open,

	f@is_File ? (
		verbose ? file_aff@printerr,

		(s:f@fgetln; f@eof || f@ferror; s=f@fgetln) !* (
			s@is_String ? (
				n:s@span_Alphabetic,
				n ? (
					cs:s <<< n,
					s = get_affix_args(s, n),
					affix_cmd[cs](htmlchar decode s)
				)
			)
		),

		f@ferror ? unable_to_read file_aff,

		f@close
	;
		unable_to_open file_aff
	),

	()
);

cmp_affix_rule_condition:[
	arc_seq(cs,cc,j): (
		match:TRUE,
		(n:cs@length, k:0;
			match && k < n;
			k++, j++
		) ?* (
			cs[k] ?: (cc: '.': ; match = FALSE)
		),
		match
	);
	arc_set(cs,cc,j): (
		j++,
		(cc, cs)@is_CharInSet
	);
	arc_not(cs,cc,j): (
		j++,
		(cc, cs)@is_NotCharInSet
	);
];

#==============================================================================

catprefix(afx,wlist,w): (
	afx@is_Environment ? (
		len:w@length,

		(R:lengthof(afx rule), r:0; r < R; r++) ?* (
			rule:afx  rule[r],
			slen:lengthof(rule strip),
			cond:rule cond,
			pref:rule affix[0],

			debug ? dump("rule", rule, 1),
			(len >= cond len
				&& (len + __(affix)FULLSTRIP) > slen
				&& (
					(!slen)
					|| rule strip == w[..(slen - 1)]
				)
			) ? (
				match:TRUE,
				(N:cond@length, i:1, j:0; match && i < N; i++) ?* (
					match = cmp_affix_rule_condition[cond[i]type](cond[i]char, w[j], j)
				),
				match ? (
					wroot:wlist@length,

					ws:w[slen..],
					wl:pref ws,
					wlist [wl] = FALSE,

					nocontin ! cataffix(rule affix[1], wlist, wroot, wl)
				)
			)
		)
	),

	()
);

catsuffix(afx,wlist,w): (
	afx@is_Environment ? (
		len:w@length,

		(R:lengthof(afx rule), r:0; r < R; r++) ?* (
			rule:afx  rule[r],
			slen:lengthof(rule strip),
			cond:rule cond,
			suff:rule affix[0],

			debug ? dump("rule", rule, 1),
			(len >= cond len
				&& (len + __(affix)FULLSTRIP) > slen
				&& (
					(!slen)
					|| rule strip == w[(len - slen)..]
				)
			) ? (
				match:TRUE,
				(N:cond@length, i:1, j:0; match && i < N; i++) ?* (
					match = cmp_affix_rule_condition[cond[i]type](cond[i]char, w[j], j)
				),
				match ? (
					wroot:wlist@length,

					ws:w[..(len - slen - 1)],
					wl:ws suff,
					wlist [wl] = afx cross,

					nocontin ! cataffix(rule affix[1], wlist, wroot, wl)
				)
			)
		)
	),

	()
);

cataffix(flag,wlist,wroot,w): (
	nosuffix ! (
		(f:flag[..()]) ?* (
			debug ? (w, " *.. ", f)@println,
			catsuffix(__(affix)SFX[f], wlist, w)
		)
	),

	nosuffix || noprefix ! (
		(f:flag[..()]) ?* (
			__(affix)PFX[f]cross ? (
				(N:wlist@length, i:wroot; i < N; i++) ?* (
					wlist[i] ? (
						wl:wlist[i]@identifier,
						debug ? (wl, " ..*.. ", f)@println,
						catprefix(__(affix)PFX[f], wlist, wl)
					)
				)
			)
		)
	),

	noprefix ! (
		(f:flag[..()]) ?* (
			debug ? (w, " ..* ", f)@println,
			catprefix(__(affix)PFX[f], wlist, w)
		)
	),

	()
);

catword(wlist,s): (
	n:(s, "\t #/", '\\')@span_NotInSet,
	w:s <<< n,

	wroot:wlist@length,
	wlist[w] = FALSE,

	noaffix ! (
		s[n] == '/' ? (
			s <<= (n + 1),
			n = (s, "\t #")@span_NotInSet,
			s <<<= n,
			__(affix)AF@length > 1 && s@is_Digit ? (
				s = __(affix)AF[s@to_Integer]
			)
		;
			s = ""
		),

		s ? (
			flag:get_affix_flags(s, __(affix)FLAG type),

			cataffix(flag, wlist, wroot, w)
		)
	),

	()
);

catfile(file,idir,odir,makeout): (
	file >>> 4 ?: (
	".DIC": ".AFF": (
			file >>= 4,
			file_aff:idir file".AFF",
			file_dic:idir file".DIC",
			makeout@is_String ? (
				file_out:odir makeout".OUT"
			;
				file_out:odir file".OUT"
			)
		);
	".dic": ".aff": (
			file >>= 4,
			file_aff:idir file".aff",
			file_dic:idir file".dic",
			makeout@is_String ? (
				file_out:odir makeout".out"
			;
				file_out:odir file".out"
			)
		);
		(
			file_aff:idir file".aff",
			file_dic:idir file".dic",
			makeout@is_String ? (
				file_out:odir makeout".out"
			;
				file_out:odir file".out"
			)
		);
	),

	makeout ? (
		verbose ? (">" file_out)@printerr,
		outfile@close,
		outfile = (file_out, "w")@open,
		outfile@is_File ! (
			unable_to_open file_out
		)
	),

	outfile@is_File ? (
		__["affix"]=AFFIX,

		noaffix ! (
			read_affix(file_aff, __(affix)),

			debug ? dump("affix", __(affix), 0)
		),

		nowords ! (
			f:file_dic@open,

			f@is_File ? (
				wlist:[],

				s:f@fgetln,
				verbose ? (file_dic, " [", s@to_Integer, "]")@printerr,

				(s:f@fgetln; f@eof || f@ferror; s=f@fgetln) !* (
					s@is_String ? (
						s[0]@is_Space ! (
							catword(wlist, htmlchar decode s)
						),
					)
				),

				f@ferror ? unable_to_read file_dic,

				f@close,

				(N:wlist@length, i:0; i < N; i++) ?* (
					(outfile, wlist[i]@identifier)@fprintln
				)
			;
				unable_to_open file_dic
			)
		)
	),

	()
);

catfiles(file,idir,odir,makeout): (
	f:file@open,

	f@is_File ? (
		(s:f@fgetln; f@eof || f@ferror; s=f@fgetln) !* (
			n:s@span_Space,
			s[n] == '-' ? ((
				_makeout :makeout,
				_noaffix :noaffix,
				_noprefix:noprefix,
				_nosuffix:nosuffix,

				o:0,

				(N:s@length; n < N && s[n] == '-') ?* (
					m:n + s((n + 1)..)@span_NotSpace,
					o:m + s((m + 1)..)@span_Space + 1,

					s[n..m] ?: (
					"-r": "--rename"  : (
							n:o,
							m:n + s((n + 1)..)@span_NotSpace,
							o:m + s((m + 1)..)@span_Space + 1,
							makeout = s[n..m],
						);
					"-n": "--noaffix" : noaffix  = TRUE;
					"-p": "--noprefix": noprefix = TRUE;
					"-s": "--nosuffix": nosuffix = TRUE;
					"-c": "--nocontin": nocontin = TRUE;
					),

					n:o + s(o..)@span_Space
				),

				catfile(s << o, idir, odir, makeout)

				nosuffix = _nosuffix,
				noprefix = _noprefix,
				noaffix  = _noaffix,
				makeout  = _makeout
			);(
				s[n] <> '#' ? (
					catfile(s << n, idir, odir, makeout)
				)
			))
		),

		f@ferror ? unable_to_read file,

		f@close
	;
		unable_to_open file
	),

	()
);

#==============================================================================

main(argc, argv) : (
	input_directory :"";
	output_directory:"";
	makeout         :FALSE;

	(continue:TRUE, i:1; continue && i < argc; i++) ?* (
		args:argv[i],

		args ?: (
		"-h": "--help"     : USAGE@println;
		"-v": "--verbose"  : verbose = TRUE;
		"-d": "--directory": (
				i + 1 < argc ? (
					i++,
					output_directory = (input_directory = argv[i])
				)
			);
		"-o": "--output"   : (
				i + 1 < argc ? (
					args = argv[i + 1],
					args ?: (
					"-d": "--directory": (
						i++,
						i + 1 < argc ? (
								i++,
								output_directory = argv[i]
							)
						)
					)
				),
				i + 1 < argc ? (
					args = argv[i + 1],
					args[0] <> '-' ? (
						makeout = (args[0] == '@'),
						makeout ! (
							i++,
							outfile@close,
							outfile = (args, "w")@open,
							continue = outfile@is_File,
							continue ! (
								unable_to_open args
							)
						)
					)
				)
			);
		"-m": "--minimal"  : minimal = TRUE;
		"-n": "--noaffix"  : noaffix = TRUE;
		"-p": "--noprefix" : noprefix = TRUE;
		"-s": "--nosuffix" : nosuffix = TRUE;
		"-c": "--nocontin" : nocontin = TRUE;
		"-w": "--nowords"  : nowords = TRUE;
		      "--version"  : VERSION@println;
		      "--debug"    : debug = TRUE;
		(args[0] == '@') ? (
			catfiles(args << 1, input_directory, output_directory, makeout)
		;
			catfile(args, input_directory, output_directory, FALSE)
		))
	;
		USAGE@printerr
	),

	outfile@close
);

main(@argc, @argv);
