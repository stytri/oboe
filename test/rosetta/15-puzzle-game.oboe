# https://rosettacode.org/wiki/15_Puzzle_Game

"rosetta/libutil"@import;

N::4;
M::N-1;

cells:[];

hole:[
	row:0,
	column:0
];

nop(x):(x);
inc(x):((x < M) ? (x+1 ; M));
dec(x):((x > 0) ? (x-1 ; 0));

dc:[ nop, nop, dec, inc ];
dr:[ dec, inc, nop, nop ];

setup(): (
	count: -1,

	cells[0..(N-1)] =[],
	(row:cells[]) ?* (
		row[0..(N-1)] = (count++)
	),

	hole(row = 0, column = 0),

	(i:0..127) ?* (
		update(select(4))
	)
);

update(move): (
	r:, c:,

	hole(
		r = dr[move](row),
		c = dc[move](column),

		(r <> row || c <> column) ? (
			cells[row][column] >< cells[r][c],

			row    = r,
			column = c
		)
	)
);

is_not_finished(): (
	return:0, count:0,

	(row:0; return == 0 && row < N; row++) ?* (
		(column:0; return == 0 && column < N; column++) ?* (
			count++,

			(cells[row][column] <> count) ? (
				return = 1
			)
		)
	),

	return
);

show_cells(): (
	``@println,
	(row:cells[]) ?* (
		(column:row[]) ?* (
			column ? (
				(" : ", itos(2, column))@print
			;
				" :   "@print
			)
		),
		" :"@println
	),
	``@println
);

get_move(): (
	s:getcmd(),
	(i:0..(s@length-1)) ?* (
		s[i] ?: (
		(`u`: update(0));
		(`d`: update(1));
		(`l`: update(2));
		(`r`: update(3))
		)
	)
);

setup();
show_cells();

(is_not_finished()) ?* (
	get_move(),
	show_cells()
);

"You won"@println;


