# https://rosettacode.org/wiki/Priority_queue

"rosetta/libutil"@import;

node(p, d): [
	priority: p,
	data    : d
];

heap(): [
	length: 0,
	nodes : [
		node(0,"")
	]
];

push(h, d, cmp): (
	i: h length + 1,
	h(
		(j: i / 2; i > 1 && cmp(nodes[j], d); j /= 2) ?* (
			nodes[i] = nodes[j],
			i = j
		),
		nodes[i] = d,
		length++
	),
);

pop(h, cmp): (
	return:(),

	n: h length,
	(n > 0) ? h(
		return = nodes[1],

		nodes[1] = nodes[n],
		length--,

		(i:1, j:, k:; i <> n) ?* (
			k = n,
			j = 2 * i,
			h(
				(j <= length && cmp(nodes[k], nodes[j])) ? (
					k = j
				),
				j++,
				(j <= length && cmp(nodes[k], nodes[j])) ? (
					k = j
				),
				nodes[i] = nodes[k]
			),
			i = k
		)
	),

	return
);

h: heap();

gt(l,r): (l priority > r priority);

push(h, node(3, "Clear drains"), gt);
push(h, node(4, "Feed cat"), gt);
push(h, node(5, "Make tea"), gt);
push(h, node(1, "Solve RC tasks"), gt);
push(h, node(2, "Tax return"), gt);

(d:, n:h length, i:0; i < n; i++) ?* (
	pop(h, gt)data@println
);

